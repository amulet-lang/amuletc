/!*
  * Program to split a string
  * @author: ruka-lang
  */

const std = @import("std")
const string = std.string

/** Takes a string and splits the string into substrings at each occurence of
  * the passed in character
  *
  * # Examples
  * ```
  * @> strsplit("Hello, world!", ' ')
  * ["Hello,", "world!"]
  * ```
  */
fn strsplit -> std.ArrayList(string) = (
    str: []u8,
    ch: u8
) {
    let mut indices = std.ArrayList(uint).new()
    for c, i in str, 0.. {
        if c == ch => indices.push(i)
    }

    let mut words = std.ArrayList(string).new()
    let mut prev = 0

    for idx in indices {
        let str = str[prev..idx].into(string)
        words.push(str)
        prev = idx + 1
    }

    let str = str[prev..str.len].into(string)
    words.push(str)

    words
}

fn main -> Result(unit) = () {
    let words =
        "| Hello,
         | world!
         |"
        |> ([..])
        |> strsplit('\n')

    @assert(words.len == 3).?

    Ok <| ()
}
